# Maintainer: Antoine Martin <antoine@ayakael.net>

# Based on PKGBUILD by the following:
# Philipp Schmitt (philipp<at>schmitt<dot>com

pkgname=wallabag
pkgver=2.3.5
pkgrel=0
pkgdesc='Self hostable application for saving web pages'
arch='noarch'
url='http://www.wallabag.org/'
license='MIT'
depends='php7 php7-gd php7-tidy pcre php7-tokenizer php7-bcmath composer'
options='!strip'
source="https://static.wallabag.org/releases/wallabag-release-${pkgver}.tar.gz"
builddir="${srcdir}/${pkgver}"

_USER=nginx
_GROUP=nginx

build() {
   return 0
}

package() {
    cd "${builddir}"

    # Make directories
    mkdir -p "${pkgdir}/usr/share/webapps/${pkgname}" "${pkgdir}/var/lib/${pkgname}" "${pkgdir}/etc/${pkgname}"

    # Copy operations
    cp . -R "${pkgdir}/usr/share/webapps/${pkgname}"
    mv "${pkgdir}/usr/share/webapps/${pkgname}/app/config/parameters.yml" "${pkgdir}/etc/${pkgname}/"
    mv "${pkgdir}/usr/share/webapps/${pkgname}/var" "${pkgdir}/var/lib/${pkgname}/"
    mv "${pkgdir}/usr/share/webapps/${pkgname}/data" "${pkgdir}/var/lib/${pkgname}/"

    # Link operations
    ln -s "/etc/${pkgname}/parameters.yml" "${pkgdir}/usr/share/webapps/${pkgname}/app/config/parameters.yml"
    ln -s "/var/lib/${pkgname}/var" "${pkgdir}/usr/share/webapps/${pkgname}/"
    ln -s "/var/lib/${pkgname}/data" "${pkgdir}/usr/share/webapps/${pkgname}/"

    # Chown operartions
    chown -R ${_USER}:${_GROUP} "${pkgdir}/etc/${pkgname}"
    chown -R ${_USER}:${_GROUP} "${pkgdir}/var/lib/${pkgname}"
    chown -R ${_USER}:${_GROUP} "${pkgdir}/usr/share/webapps/${pkgname}"

    sed -i "${pkgdir}/var/lib/${pkgname}/var/bootstrap.php.cache" -e "s@__DIR__.'/../@'/usr/share/webapps/${pkgname}/@"

}
sha512sums="46cb5c9c435bfc6e909111d294688bbddcdcd360792834d193dd538f81617c7cd1dc51fb7c6599d44d20a869b92cd4bca7520484542c9623069bab0b12c881d0  wallabag-release-2.3.5.tar.gz"
